plugins {
    id 'war'
}

description "Waveform Viewer and Browsing App for CEBAF"
group 'org.jlab'
version '2.1.1'
ext.releaseDate = 'May 3, 2024'
ext.resourceNumber = '40'
ext.adminRole = 'wfb_admin'
ext.postRole = 'wfb_data'

tasks.withType(JavaCompile) {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.tuckey:urlrewritefilter:4.0.4'
    implementation 'org.apache.commons:commons-compress:1.18'
    implementation 'org.jlab:smoothness-weblib:4.5.2'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'mysql:mysql-connector-java:8.0.12'
    testImplementation 'org.glassfish:javax.json:1.1.4'
    testImplementation 'org.mariadb.jdbc:mariadb-java-client:3.0.8'
    providedCompile 'javax:javaee-api:8.0.1'
    providedCompile 'javax.servlet:jstl:1.2'
}

war {
    archiveName "wfbrowser.war"
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll('@VERSION@', project.version)
        }
        filter {
            String line -> line.replaceAll('@RELEASE_DATE@', releaseDate)
        }
        filter {
            String line -> line.replaceAll('@RESOURCE_NUMBER@', resourceNumber)
        }
        filter {
            String line -> line.replaceAll('@ADMIN_ROLE@', adminRole)
        }
        filter {
            String line -> line.replaceAll('@POST_ROLE@', postRole)
        }
    }
}

test {
    environment "WFB_DATA_DIR", "${project.projectDir}/data"
    useJUnit()
    maxHeapSize = '1G'
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}